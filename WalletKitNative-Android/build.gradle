apply plugin: 'com.android.library'
apply from: '../gradle/publish.gradle'

def activateAndroid = gradle.startParameter.taskNames.any {  it.contains("-Android") }
def disableIdeaCheck = activateAndroid || property("disableIdeaCheck") == "true"
def ideaActive = !disableIdeaCheck && System.getProperty("idea.active") == "true"

android {
    compileSdkVersion androidConfig.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        versionCode 1
        versionName version

        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild.cmake.arguments "-DANDROID_STL=none", "-DANDROID_TOOLCHAIN=clang"
        ndk.abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
    }

    externalNativeBuild {
        cmake {
            path project(':WalletKitNative').cmakeCMakeListsFile
            version versions.cmake
        }
    }

    lintOptions {
        quiet false
        explainIssues true
        abortOnError true
        ignoreWarnings false
        checkReleaseBuilds false
    }

    sourceSets {
        main {
            if (!ideaActive) {
                java {
                    srcDirs = [project(':WalletKitNative').javaMainSrcDir]
                }
            }
        }
    }
}

dependencies {
    api deps.jnaAndroid
    api deps.guavaAndroid

    testImplementation deps.junit

    androidTestImplementation deps.androidxTestRunner
    androidTestImplementation deps.androidxTestEspresso
}
