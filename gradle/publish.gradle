apply plugin: 'com.jfrog.bintray'

def isAndroidLibrary = plugins.hasPlugin('com.android.library')
if (isAndroidLibrary) {
    plugins.apply('digital.wup.android-maven-publish')
} else {
    plugins.apply('maven-publish')
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    afterEvaluate {
        if (isAndroidLibrary) {
            from android.sourceSets.main.java.srcDirs
        } else {
            from sourceSets.main.allSource
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact sourcesJar
            if (isAndroidLibrary) {
                from components.android
            } else {
                from components.findByName("kotlin") ?: components.java
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'walletkit-java'
        name = project.name
        userOrg = 'brd'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/blockset-corp/walletkit-java.git'
        githubRepo = 'blockset-corp/walletkit-java'
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.property('version')
            released = new Date()
            vcsTag = project.property('version')
        }
    }
}
