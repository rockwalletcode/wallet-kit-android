apply plugin: 'maven-publish'

def isAndroidLibrary = plugins.hasPlugin('com.android.library')

task sourcesJar(type: Jar) {
    classifier 'sources'
    afterEvaluate {
        if (isAndroidLibrary) {
            from android.sourceSets.main.java.srcDirs
        } else {
            from sourceSets.main.allSource
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.rockwalletcode'
            artifactId project.name.toLowerCase()
            version project.wkVersion
            artifact sourcesJar

            if (isAndroidLibrary) {
                artifact("$buildDir/outputs/aar/${project.name}-release.aar")
            } else {
                from components.findByName("kotlin") ?: components.java
            }
        }
    }
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/rockwalletcode/wallet-kit-android")

            credentials {
                username = project.findProperty("rw.user");
                password = project.findProperty("rw.token");
            }
        }
    }
}
