apply plugin: 'maven-publish'

def isAndroidLibrary = plugins.hasPlugin('com.android.library')

task sourcesJar(type: Jar) {
    classifier 'sources'
    afterEvaluate {
        if (isAndroidLibrary) {
            from android.sourceSets.main.java.srcDirs
        } else {
            from sourceSets.main.allSource
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.rockwalletcode'
            artifactId project.name
            version project.wkVersion
            artifact sourcesJar

            if (isAndroidLibrary) {
                artifact("$buildDir/outputs/aar/${project.name}-release.aar")
            } else {
                from components.findByName("kotlin") ?: components.java
            }
        }
    }
    repositories {
        maven {
            url = uri(System.getProperty('user.home') + '/.m2/repository')
        }
    }
}
